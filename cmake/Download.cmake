MESSAGE(STATUS "Include Download.cmake")

FUNCTION(DOWNLOAD_FILE URL FILENAME HASH_TYPE HASH)
    IF (EXISTS ${FILENAME})
        FILE(${HASH_TYPE} ${FILENAME} ORIG_FILE_HASH)
        STRING(TOLOWER ${HASH} LHASH)
        STRING(TOLOWER ${ORIG_FILE_HASH} LORIG_FILE_HASH)
        IF (${LHASH} STREQUAL ${LORIG_FILE_HASH})
            MESSAGE(STATUS "Found File: ${FILENAME} with same ${HASH_TYPE}")
            RETURN()
        ELSE ()
            MESSAGE(STATUS "Found File: ${FILENAME} with different ${HASH_TYPE}")
        ENDIF ()
    ENDIF ()
    MESSAGE(STATUS "Download File: ${FILENAME}")
    FILE(DOWNLOAD ${URL} ${FILENAME}
            TIMEOUT 60  # seconds
            EXPECTED_HASH ${HASH_TYPE}=${HASH}
            TLS_VERIFY ON)
ENDFUNCTION()

FUNCTION(DOWNLOAD_TEST_FILE URL FILENAME HASH_TYPE HASH)
    IF ("${BUILD_TEST}" STREQUAL "OFF" OR "${BUILD_TEST}" STREQUAL "")
        RETURN()
    ENDIF ()
    DOWNLOAD_FILE(${URL} ${FILENAME} ${HASH_TYPE} ${HASH})
ENDFUNCTION()